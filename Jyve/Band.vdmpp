/* Represents the public profile of a band. */
class Band is subclass of PublicUser
types
	public static Members = set1 of Artist
		inv m == card m >= minMembers;
	public static Bands = set of Band;
values
	private static minMembers = 2;
instance variables
	public members : Members;
	public static bands : Bands := {};
	inv forall band in set bands & (forall artist in set band.members & band in set artist.memberOf);
operations
	public Band : Common`String1 * Common`Address * Members ==> Band
	Band(stgName,addr,m) == (
		members := m;
		for all member in set members do
			member.AddBand(self);
		bands := bands union {self};
		PublicUser(stgName,addr);
	)
	pre card m >= minMembers	-- Verification of the invariant.
	post members = m	-- Verification of the invariant by verifying that the assignment is correct (already passed precondition).
		and forall member in set members & self in set member.memberOf
		and self in set bands;
functions
traces
end Band