class Band is subclass of PublicUser
types
	public static Members = set1 of Artist
		inv m == card m >= minMembers;
	public static Bands = set of Band;
values
	private static minMembers = 2;
instance variables
	public members : Members;
	public static bands : Bands := {};
operations
	public Band : Common`String1 * Common`Address * Members ==> Band
	Band(stgName,loc,m) == (
		members := m;
		for all member in set members do
			member.AddBand(self);
		bands := bands union {self};
		PublicUser(stgName,loc);
	)
	pre card m >= minMembers	-- invariant verification
	post members = m	-- invariant verification by verifying that the assignment is correct (already passed precondition)
		and forall member in set members & self in set member.memberOf
		and self in set bands;

	public AddMember : Artist ==> ()
	AddMember(a) == members := members union {a}
	pre a not in set members
	post a in set members and members~ = members \ {a};
	
	public RemoveMember : Artist ==> ()
	RemoveMember(a) == members := members \ {a}
	pre a in set members and card members > minMembers
	post a not in set members and members~ = members union {a};
functions
traces
end Band