class Artist is subclass of PublicUser
types
	public static Bands = set of Band;
	public static Artists = set of Artist;
values
instance variables
	public memberOf : Bands;
	public static artists : Artists := {};
operations
	public Artist : Common`String1 * Common`Address ==> Artist
	Artist(stgName,loc) == (
		memberOf := {};
		artists := artists union {self};
		PublicUser(stgName,loc);
	)
	post memberOf = {} and artists~ = artists \ {self};
	
	public AddBand : Band ==> ()
	AddBand(b) == memberOf := memberOf union {b}
	pre b not in set memberOf
	post b in set memberOf and memberOf~ = memberOf \ {b};
	
	public RemoveBand : Band ==> ()
	RemoveBand(b) == memberOf := memberOf \ {b}
	pre b in set memberOf
	post b not in set memberOf and memberOf~ = memberOf union {b};
functions
traces
end Artist